# Created by newuser for 5.6.1

. "$HOME/.nix-profile/etc/profile.d/hm-session-vars.sh"

if [ -n "${commands[fzf-share]}" ]; then
  source "$(fzf-share)/key-bindings.zsh"
  source "$(fzf-share)/completion.zsh"

  if [ -n "${commands[fd]}" ]; then
    export FZF_DEFAULT_COMMAND="fd -L 2>/dev/null"
    export FZF_ALT_C_COMMAND="fd -L -t d 2>/dev/null"
    export FZF_CTRL_T_COMMAND="fd -L -t f -t l 2>/dev/null"
  fi

  bindkey "^I" $fzf_default_completion
fi

if [ -n "${commands[rg]}" ] && [ -n "${commands[fzf]}" ]; then
  function __rg_fzf_edit_widget() {
    emulate -L zsh
    setopt localoptions pipefail no_aliases

    local selection file line editor rg_cmd
    rg_cmd="rg --vimgrep --color=always --smart-case --hidden --glob '!.git/*'"
    selection=$(
      fzf --ansi --phony --query "" --prompt "rg> " \
        --bind "start:reload:${rg_cmd} -- {q} || true" \
        --bind "change:reload:${rg_cmd} -- {q} || true"
    ) || return

    file="${selection%%:*}"
    line="${selection#*:}"
    line="${line%%:*}"
    editor="${EDITOR:-nvim}"
    BUFFER="${editor} +${line} ${(q)file}"
    CURSOR=${#BUFFER}
  }

  zle -N __rg_fzf_edit_widget
  bindkey "^F" __rg_fzf_edit_widget
fi

export PATH="/home/william/.william/etc/bin:/home/william/.cargo/bin:/home/william/go/bin:/home/william/.npm/bin:$PATH"
export GOPATH="/home/william/go"
